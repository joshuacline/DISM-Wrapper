:IMAGEPROC_START
@ECHO OFF&&SETLOCAL ENABLEDELAYEDEXPANSION&&CD /D %~DP0
Reg.exe query "HKU\S-1-5-19\Environment">NUL
IF NOT %ERRORLEVEL% EQU 0 ECHO Right-Click ^& Run As Administrator&&PAUSE&&EXIT 0
SET "PROG_SOURCE=%CD%"&&SET "PROG_TARGET=%CD%"&&SET "IMAGE_FOLDER=%CD%"
IF NOT EXIST "%IMAGE_FOLDER%\%VHDX_SOURCE%" CALL SET VHDX_SOURCE=-SELECT-
IF NOT EXIST "%IMAGE_FOLDER%\%WIM_SOURCE%" CALL SET WIM_SOURCE=-SELECT-
FOR %%a in (VHDX_SOURCE WIM_SOURCE WIM_TARGET VHDX_TARGET) DO (IF NOT DEFINED %%a SET "%%a=-SELECT-")
IF "%WIM_SOURCE%"=="-SELECT-" SET WIM_DESC=NULL&&SET WIM_INDEX=NULL
IF NOT "%WIM_SOURCE%"=="-SELECT-" IF "%WIM_INDEX%"=="NULL" SET WIM_INDEX=1
CLS&&TITLE  Download from github.com/joshuacline&&COLOR 0A&&CALL:PAD_LINE&&ECHO             Josh's Image-Processing DISM Command-Wrapper&&CALL:PAD_LINE
IF NOT DEFINED WIM_COMPRESS SET WIM_COMPRESS=FAST&&SET SOURCE_SLOT=WIM&&SET TARGET_SLOT=VHDX&&SET VHDX_SIZE=25600
IF NOT DEFINED PROG_OPER ECHO  Example Use Cases :
IF NOT DEFINED PROG_OPER IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="VHDX" ECHO   WIM To VHDX : Restore Windows-Image to VHDX
IF NOT DEFINED PROG_OPER IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="VHDX" ECHO   WIM To VHDX : Restore Backup-Image to VHDX
IF NOT DEFINED PROG_OPER IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="WIM" ECHO   WIM To WIM  : Isolate Index
IF NOT DEFINED PROG_OPER IF "%SOURCE_SLOT%"=="VHDX" IF "%TARGET_SLOT%"=="WIM" ECHO  VHDX To WIM  : Create Backup-Image of VHDX
IF NOT DEFINED PROG_OPER CALL:PAD_LINE
IF NOT DEFINED PROG_OPER IF "%SOURCE_SLOT%"=="VHDX" ECHO. {S}OURCE-VHDX] [%VHDX_SOURCE%]
IF NOT DEFINED PROG_OPER IF "%SOURCE_SLOT%"=="WIM" ECHO. {S}OURCE-WIM]  [%WIM_SOURCE%] {I}ndex[%WIM_INDEX%] [Edition[%WIM_DESC%]
IF NOT DEFINED PROG_OPER CALL:PAD_LINE
IF NOT DEFINED PROG_OPER IF "%TARGET_SLOT%"=="VHDX" ECHO. {T}ARGET-VHDX] [%VHDX_TARGET%] {V}Size [MB[%VHDX_SIZE%]
IF NOT DEFINED PROG_OPER IF "%TARGET_SLOT%"=="WIM" ECHO. {T}ARGET-WIM]  [%WIM_TARGET%] {Z}[X-LVL[%WIM_COMPRESS%]
IF "%PROG_OPER%"=="PICK" IF "%SOURCE_SLOT%"=="WIM" SET LIST_FMT=NUMERAL&&SET "LIST_DIR=%IMAGE_FOLDER%\*.WIM"&&ECHO   AVAILABLE WIM'S:&&ECHO.&&CALL:FILE_LIST
IF "%PROG_OPER%"=="PICK" IF "%SOURCE_SLOT%"=="VHDX" SET LIST_FMT=NUMERAL&&SET "LIST_DIR=%IMAGE_FOLDER%\*.VHDX"&&ECHO   AVAILABLE VHDX'S:&&ECHO.&&CALL:FILE_LIST
IF NOT DEFINED PROG_OPER CALL:PAD_LINE
IF NOT DEFINED PROG_OPER ECHO  {X} [%SOURCE_SLOT%]To[%TARGET_SLOT%]             {G}o^^!&&CALL:PAD_LINE
IF DEFINED PROG_OPER CALL:PAD_LINE&&ECHO                    Enter File # To [%PROG_OPER%] {/}Type&&CALL:PAD_LINE
ECHO                Press (Enter) to Return to Previous Menu
CALL:MENU_SELECT&&IF DEFINED PROG_OPER CALL:FILE_LIST>NUL
IF "%SELECT%"=="G" SET PROG_OPER=GOTIME
IF "%SELECT%"=="I" CALL:WIM_INDEX
IF "%SELECT%"=="S" SET PROG_OPER=PICK&&GOTO:IMAGEPROC_START
IF "%SELECT%"=="T" SET PROG_OPER=PROMPT_SET
IF "%SELECT%"=="V" SET PROG_OPER=VSIZE
IF "%SELECT%"=="X" CALL:IMAGEPROC_SLOT
IF "%SELECT%"=="Z" CALL:WIM_COMPRESS
IF "%PROG_OPER%"=="VSIZE" SET PROMPT_SET=VHDX_SIZE&&ECHO VHDX Size?&&CALL:PROMPT_SET
IF "%PROG_OPER%"=="PICK" IF "%SOURCE_SLOT%"=="WIM" CALL SET "WIM_SOURCE=%SELECT%"&&SET WIM_INDEX=1&&CALL:WIM_INDEX_QUERY
IF "%PROG_OPER%"=="PICK" IF "%SOURCE_SLOT%"=="VHDX" CALL SET "VHDX_SOURCE=%SELECT%"
IF "%PROG_OPER%"=="GOTIME" SET CAME_FROM=IMAGE&&CALL:IMAGEPROC&&PAUSE
IF "%PROG_OPER%"=="PROMPT_SET" IF "%TARGET_SLOT%"=="WIM" ECHO Name of WIM?&&CALL SET "PROMPT_SET=WIM_TARGET"&&CALL:PROMPT_SET
IF "%PROG_OPER%"=="PROMPT_SET" IF "%TARGET_SLOT%"=="WIM" IF DEFINED WIM_TARGET CALL SET "WIM_TARGET=%WIM_TARGET%.WIM"
IF "%PROG_OPER%"=="PROMPT_SET" IF "%TARGET_SLOT%"=="VHDX" ECHO Name of VHDX?&&CALL SET "PROMPT_SET=VHDX_TARGET"&&CALL:PROMPT_SET
IF "%PROG_OPER%"=="PROMPT_SET" IF "%TARGET_SLOT%"=="VHDX" IF DEFINED VHDX_TARGET CALL SET "VHDX_TARGET=%VHDX_TARGET%.VHDX"
IF DEFINED PROG_OPER SET PROG_OPER=
GOTO:IMAGEPROC_START
:IMAGEPROC_SLOT
IF NOT DEFINED IMAGEPROC_SLOT SET IMAGEPROC_SLOT=1
SET /A IMAGEPROC_SLOT+=1
IF "%IMAGEPROC_SLOT%"=="4" SET IMAGEPROC_SLOT=1
IF "%IMAGEPROC_SLOT%"=="1" SET SOURCE_SLOT=WIM&&SET TARGET_SLOT=VHDX
IF "%IMAGEPROC_SLOT%"=="2" SET SOURCE_SLOT=VHDX&&SET TARGET_SLOT=WIM
IF "%IMAGEPROC_SLOT%"=="3" SET SOURCE_SLOT=WIM&&SET TARGET_SLOT=WIM
EXIT /B
:WIM_COMPRESS
SET SELECT=
IF NOT DEFINED WIM_XLVL SET WIM_XLVL=1
SET /A WIM_XLVL+=1
IF "%WIM_XLVL%"=="4" SET WIM_XLVL=1
IF "%WIM_XLVL%"=="1" SET WIM_COMPRESS=FAST
IF "%WIM_XLVL%"=="2" SET WIM_COMPRESS=MAX
IF "%WIM_XLVL%"=="3" SET WIM_COMPRESS=NONE
EXIT /B
:WIM_INDEX
SET SELECT=&&SET WIM_DESC=&&SET /A WIM_INDEX+=1
IF "%WIM_INDEX%"=="15" SET WIM_INDEX=1
IF EXIST "%IMAGE_FOLDER%\%WIM_SOURCE%" CALL:WIM_INDEX_QUERY
IF NOT DEFINED WIM_DESC SET WIM_INDEX=1&&CALL:WIM_INDEX_QUERY
EXIT /B
:WIM_INDEX_QUERY
Dism /Get-ImageInfo /ImageFile:"%IMAGE_FOLDER%\%WIM_SOURCE%" /Index:%WIM_INDEX% >"%TEMP%\WIMINFO"
FOR /F "tokens=1-5 delims= " %%a in (%TEMP%\WIMINFO) DO (IF "%%a"=="Edition" SET WIM_DESC=%%c)
FOR /F "tokens=1-5 delims= " %%a in (%TEMP%\WIMINFO) DO (IF "%%a"=="Languages" IF NOT "%%c"=="" SET WIM_SOURCE=-SELECT-&&SET WIM_DESC=NULL)
DEL /Q /F "%TEMP%\WIMINFO" >NUL
EXIT /B
:IMAGEPROC
IF "%SOURCE_SLOT%"=="VHDX" IF "%VHDX_SOURCE%"=="-SELECT-" ECHO &&ECHO SOURCE %SOURCE_SLOT% DOESN'T EXIST&&PAUSE&&EXIT /B
IF "%TARGET_SLOT%"=="VHDX" IF "%VHDX_TARGET%"=="-SELECT-" ECHO &&ECHO TARGET %TARGET_SLOT% NOT SET&&PAUSE&&EXIT /B
IF "%SOURCE_SLOT%"=="WIM" IF "%WIM_SOURCE%"=="-SELECT-" ECHO &&ECHO SOURCE %SOURCE_SLOT% NOT SET&&PAUSE&&EXIT /B
IF "%TARGET_SLOT%"=="WIM" IF "%WIM_TARGET%"=="-SELECT-" ECHO &&ECHO TARGET %TARGET_SLOT% NOT SET&&PAUSE&&EXIT /B
IF NOT DEFINED WIM_INDEX SET WIM_INDEX=1
CALL:PAD_LINE&&ECHO                         Image-Processing Start&&CALL:PAD_LINE
SET "APPLYDIR_MASTER=Z:"&&SET "CAPTUREDIR_MASTER=Z:"
SET "SCRATCHDIR=%PROG_TARGET%\Scratch"
CALL:VDISK_DETACH>NUL 2>&1
IF EXIST "%SCRATCHDIR%" DISM /cleanup-MountPoints>NUL 2>&1
IF EXIST "%SCRATCHDIR%" RD /S /Q "\\?\%SCRATCHDIR%">NUL 2>&1
IF NOT EXIST "%SCRATCHDIR%" MD "%SCRATCHDIR%">NUL 2>&1
CALL:EMPTY_TRASH>NUL 2>&1
IF "%SOURCE_SLOT%"=="VHDX" SET "VDISK=%IMAGE_FOLDER%\%VHDX_SOURCE%"
IF "%TARGET_SLOT%"=="VHDX" SET "VDISK=%IMAGE_FOLDER%\%VHDX_TARGET%"
IF "%SOURCE_SLOT%"=="WIM" SET "IMAGEFILE_SRC=%IMAGE_FOLDER%\%WIM_SOURCE%"
IF "%TARGET_SLOT%"=="WIM" SET "IMAGEFILE_TGT=%IMAGE_FOLDER%\%WIM_TARGET%"
IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="WIM" SET "VDISK=%SCRATCHDIR%\SCRATCH.VHDX"&&CALL:VDISK_CREATE
IF "%SOURCE_SLOT%"=="VHDX" IF "%TARGET_SLOT%"=="WIM" CALL:VDISK_ATTACH
IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="VHDX" SET VHDX_SIZE=%VHDX_SIZE%&&CALL:VDISK_CREATE
IF NOT DEFINED WIM_DESC SET WIM_DESC=$HAZZAM
IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="VHDX" DISM /APPLY-IMAGE /IMAGEFILE:"%IMAGEFILE_SRC%" /INDEX:%WIM_INDEX% /APPLYDIR:"%APPLYDIR_MASTER%"
IF "%SOURCE_SLOT%"=="WIM" IF "%TARGET_SLOT%"=="WIM" DISM /APPLY-IMAGE /IMAGEFILE:"%IMAGEFILE_SRC%" /INDEX:%WIM_INDEX% /APPLYDIR:"%APPLYDIR_MASTER%"
IF "%TARGET_SLOT%"=="WIM" DISM /CAPTURE-IMAGE /CAPTUREDIR:"%CAPTUREDIR_MASTER%" /IMAGEFILE:"%IMAGEFILE_TGT%" /COMPRESS:%WIM_COMPRESS% /NAME:%WIM_DESC%
IF EXIST "%SCRATCHDIR%" DISM /cleanup-MountPoints>NUL 2>&1
IF EXIST "%SCRATCHDIR%" RD /S /Q "\\?\%SCRATCHDIR%">NUL 2>&1
CALL:VDISK_DETACH>NUL 2>&1
CALL:PAD_LINE&&ECHO                       Image-Processing Complete&&CALL:PAD_LINE&&ECHO 
EXIT /B
:PAD_LINE
ECHO ====================================================================
EXIT /B
:MENU_SELECT
SET SELECT=&&SET /P "SELECT=$>>"&&FOR %%G in (A B C D E F G H I J K L M N O P Q R S T U V W X Y Z) DO (CALL SET "SELECT=%%SELECT:%%G=%%G%%")
EXIT /B
:PROMPT_SET
ECHO.&&SET PROMPT_VAR=&&SET /P "PROMPT_VAR=$>>"
CALL SET "%PROMPT_SET%=%PROMPT_VAR%"&&SET PROMPT_SET=&&SET PROMPT_VAR=
EXIT /B
:FILE_LIST
IF DEFINED LIST_DIR IF EXIST "%LIST_DIR%" SET LIST_CNT=0&&DIR "%LIST_DIR%" /A: /B /O:GN>%TEMP%\$HZ.TXT&&FOR /F "TOKENS=*" %%a in (%TEMP%\$HZ.TXT) DO (CALL SET /A LIST_CNT+=1&&CALL SET "OBJ_NAME=%%a"&&CALL:FILE_LIST_OUTSIDE)
IF DEFINED LIST_DIR IF NOT EXIST "%LIST_DIR%" ECHO  [CRICKETS..]
ECHO.&&SET LIST_DIR=&&SET LIST_TXT=&&SET LIST_FMT=&&SET FILE_LIST_EXT=&&IF EXIST "%TEMP%\$HZ.TXT" DEL /F "%TEMP%\$HZ.TXT">NUL 2>&1
IF "%SELECT%"=="\" CALL:MENU_SELECT&&EXIT /B
IF "%SELECT%"=="/" CALL:MENU_SELECT&&EXIT /B
CALL SET "SELECT=%%TEMP_NAME%SELECT%%%"
CALL SET "LIST_OUTPUT=%%TEMP_NAME%SELECT%%%"
EXIT /B
:FILE_LIST_OUTSIDE
CALL SET "TEMP_NAME%LIST_CNT%=%OBJ_NAME%"
IF "%LIST_FMT%"=="NUMERAL" ECHO  [ %LIST_CNT% ]\[%OBJ_NAME%]
IF "%LIST_FMT%"=="PLAIN" ECHO  [SRC]\[%OBJ_NAME%]
EXIT /B
:VDISK_CREATE
CALL:VDISK_DETACH>NUL 2>&1
IF DEFINED NEW_VDISK SET VHDX_LABEL=%NEW_VDISK%
IF NOT DEFINED VHDX_SIZE SET VHDX_SIZE=25600
IF NOT DEFINED VHDX_LABEL SET VHDX_LABEL=VHDX%VHDX_SLOT%
DEL /Q /F "%VDISK%">NUL 2>&1
(ECHO.create vdisk file="%VDISK%" maximum=%VHDX_SIZE% type=expandable&&ECHO.Select vdisk file="%VDISK%"&&ECHO.Attach vdisk&&ECHO.create partition primary&&ECHO.select partition 1&&ECHO.format fs=ntfs quick label="%VHDX_LABEL%"&&ECHO.Assign letter=Z noerr&&ECHO.Exit)>"%TEMP%\DISK_PART"
DISKPART /s "%TEMP%\DISK_PART">NUL 2>&1
DEL "%TEMP%\DISK_PART">NUL 2>&1
EXIT /B
:VDISK_ATTACH
IF NOT DEFINED DISK_LETTER SET DISK_LETTER=Z
SET "DISK_LETTER_BAK=%DISK_LETTER%"&&CALL:VDISK_DETACH>NUL 2>&1
SET "DISK_LETTER=%DISK_LETTER_BAK%"
(ECHO.Select vdisk file="%VDISK%"&&ECHO.Attach vdisk&&ECHO.select partition 1&&ECHO.Assign letter=%DISK_LETTER% noerr&&ECHO.Exit)>"%TEMP%\DISK_PART"
DISKPART /s "%TEMP%\DISK_PART">NUL 2>&1
DEL "%TEMP%\DISK_PART">NUL 2>&1
SET DISK_LETTER=
EXIT /B
:VDISK_DETACH
IF NOT DEFINED DISK_LETTER SET DISK_LETTER=Z
(ECHO.Select vdisk file="%VDISK%"&&ECHO.Detach vdisk&&ECHO.Exit)>"%TEMP%\DISK_PART"
DISKPART /s "%TEMP%\DISK_PART">NUL 2>&1
DEL "%TEMP%\DISK_PART">NUL 2>&1
IF EXIST "%DISK_LETTER%:\" (ECHO.select VOLUME=%DISK_LETTER%&&ECHO.remove all dismount noerr&&ECHO.Exit)>"%TEMP%\DISK_PART"
DISKPART /s "%TEMP%\DISK_PART">NUL 2>&1
DEL "%TEMP%\DISK_PART">NUL 2>&1
SET DISK_LETTER=
EXIT /B